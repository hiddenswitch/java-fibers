plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'signing'
  id 'com.gradle.plugin-publish' version '0.14.0'
}

apply from: "${rootDir}/gradle/signing.gradle"

group 'com.hiddenswitch'
version '1.0.4'

evaluationDependsOn(':quasar-core')

repositories {
  mavenCentral()
}

groovydoc {
  exclude('**/*')
}

java {
  withSourcesJar()
  withJavadocJar()
  targetCompatibility(JavaVersion.VERSION_1_9)
  sourceCompatibility(JavaVersion.VERSION_1_9)
}

configurations.all {
  resolutionStrategy.dependencySubstitution {
    substitute module("com.hiddenswitch:quasar-core") using project(":quasar-core") because "we work with the unreleased development version"
  }
}

dependencies {
  implementation "com.hiddenswitch:quasar-core:10.0.3"
  implementation localGroovy()
}

pluginDescriptors.dependsOn ':quasar-core:publishQuasarCorePublicationToMavenLocal'

gradlePlugin {
  plugins {
    instrumentFibersPlugin {
      id = 'com.hiddenswitch.fibers.instrument'
      displayName = 'Java Fibers instrumentation plugin'
      description = 'A plugin to instrument your project\'s classes for fibers.'
      implementationClass = 'com.hiddenswitch.fibers.gradle.FiberInstrumentationPlugin'
    }
  }
}

pluginBundle {
  website = 'https://hiddenswitch.com'
  vcsUrl = 'https://github.com/hiddenswitch/java-fibers/tree/master/gradle-plugin'
  tags = ['quasar', 'quasar-core', 'fibers', 'coroutines', 'instrumentation', 'loom']
}

task install {
  dependsOn 'publishToMavenLocal'
}