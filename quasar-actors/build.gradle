plugins {
  id 'java-library'
  id 'maven-publish'
}

group = 'com.hiddenswitch.fibers'
version = '10.0.0'

evaluationDependsOn(':quasar-core')

repositories {
  mavenLocal()
  maven { url "https://oss.sonatype.org/content/repositories/releases" }
  maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

java {
  withSourcesJar()
}

sourceSets {
  jmh {}
}

configurations {
  jmhImplementation.extendsFrom(testImplementation)
  jmhRuntimeOnly.extendsFrom(testRuntimeOnly)
}

dependencies {
  api project(path: ':quasar-core', configuration: 'shadowedJar')

  implementation 'org.slf4j:slf4j-api:1.7.30'
  implementation "net.bytebuddy:byte-buddy:1.9.2"

  implementation("com.google.guava:guava:26.+") {
    exclude group: 'com.google.errorprone', module: '*'
    exclude group: 'org.checkerframework', module: 'checker-qual'
  }

  implementation 'com.google.errorprone:error_prone_annotations:2.4.0'

  testImplementation 'org.hamcrest:hamcrest:2.2'
  testImplementation('junit:junit:4.12') {
    exclude group: 'org.hamcrest', module: '*'
  }

  jmhImplementation 'org.openjdk.jmh:jmh-core:1.21'
  jmhImplementation 'org.openjdk.jmh:jmh-generator-annprocess:1.21'

  // todo: since this is relocated, shouldn't we also use the relocated version?
  implementation "org.ow2.asm:asm:$asmVer"
  implementation "org.ow2.asm:asm-analysis:$asmVer"
  implementation "org.ow2.asm:asm-commons:$asmVer"
  implementation "org.ow2.asm:asm-util:$asmVer"

  testRuntimeOnly "net.bytebuddy:byte-buddy:1.11.13" // for Mockito
  testImplementation('org.mockito:mockito-core:3.12.4') {
    exclude group: "org.ow2.asm", module: '*'
    exclude group: "net.bytebuddy", module: '*'
  }
}

test {
  dependsOn ':quasar-core:shadowJar'
  useJUnit()
  jvmArgs "-javaagent:${project(':quasar-core').tasks.getByName('shadowJar').outputs.files.singleFile}"
}

task install {
  dependsOn 'publishToMavenLocal'
}